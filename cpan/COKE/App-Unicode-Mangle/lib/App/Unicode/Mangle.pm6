unit class App::Unicode::Mangle:ver<1.0.1>;

my (%hacks, %posts);

sub mangle(Str $input, :$hack = 'circle') is export {
    die "invalid hack, must be one of: " ~ %hacks.keys.sort
        unless %hacks{$hack}:exists;

    my $result = $input.comb.map({
        one-char($hack, $_);
    }).join;
    
    if %posts{$hack}:exists {
        $result = %posts{$hack}($result);
    };
    $result;
}

sub one-char($hack, $char) {
    my $mod = %hacks{$hack};
    my $new-char;

    my $try-char = $char.samemark('a');
    given $mod {
        when Callable {
            $new-char = $mod($try-char);
        }
        when Associative {
            $new-char = $mod{$try-char};
        }
    }

    # Missed? try with original marks.
    if !$new-char.DEFINITE {
        given $mod {
            when Callable {
                $new-char = $mod($char);
            }
            when Associative {
                $new-char = $mod{$char};
            }
        }
    }

    # Didn't work? pass through original char
    $new-char //= $char;

    # Now add in the marks from the original character.
    # But, cheat; don't do this for the one character
    # we know that starts out with a mark but transforms
    # into something without
    if $char ne "ÎŒ" {
        my @combinors = $char.NFD.list;
        @combinors.shift;
        for @combinors -> $mark {
            $new-char ~= chr($mark);
        }
    }

    $new-char;
}

sub try-some(Str $char, Int $count) {
    state @combinors = (^1000).grep({
        uniprop($_, 'Canonical_Combining_Class') ne "0"
    }).map({.chr});
    $char ~ @combinors.pick($count).join;
}

BEGIN %hacks = (
    'random' => -> $char {
        one-char(%hacks.keys.grep({$_ ne "random"}).pick(1), $char);
    },
    'circle' => -> $char {
        try ('CIRCLED ' ~ $char.uniname).parse-names;
    },
    'paren' => -> $char {
        try ('PARENTHESIZED ' ~ $char.uniname).parse-names;
    },
    'bold' => -> $char {
        my $name = $char.uniname;
        $name ~~ s/ 'LATIN ' //;
        $name ~~ s/ 'LETTER ' //;
        $name = "MATHEMATICAL BOLD $name";
        try $name.parse-names;
    },
    'outline' => -> $char {
        my $name = $char.uniname;
        $name ~~ s/ 'LATIN ' //;
        $name ~~ s/ 'LETTER ' //;
        $name = "DOUBLE-STRUCK $name";
        my $try = try $name.parse-names;
        $try //= try ('MATHEMATICAL ' ~ $name).parse-names;
    },
    'combo' => -> $char {
        my $suggest = try-some($char, 2);
        while $suggest.uninames.grep(/'<reserved>'/) {
            $suggest = try-some($char, 2);
        }
        $suggest;
    },
    # Original table courtesy
    # http://www.fileformat.info/convert/text/upside-down-map.htm

    'invert' => -> $char {
        my $result;
        my %mappings = %(
            "!", "Â¡", '"', "â€", "&", "â…‹", "'", ",", "(", ")",
            ".", "Ë™", "3", "Æ", "4", "á”­", "6", "9", "7", "â±¢",
            ";", "Ø›", "<", ">", "?", "Â¿", "A", "âˆ€", "B", "ğ’",
            "C", "â†ƒ", "D", "â—–", "E", "Æ", "F", "â„²", "G", "â…",
            "J", "Å¿", "K", "â‹Š", "L", "â…‚", "M", "W", "N", "á´",
            "P", "Ô€", "Q", "ÎŒ", "R", "á´š", "T", "âŠ¥", "U", "âˆ©",
            "V", "á´§", "Y", "â…„", "[", "]", "_", "â€¾", "a", "É",
            "b", "q", "c", "É”", "e", "Ç", "f", "ÉŸ", "g", "Æƒ",
            "h", "É¥", "i", "Ä±", "j", "É¾", "k", "Ê", "l", "Êƒ",
            "m", "É¯", "n", "u", "p", "d", "r", "É¹", "t", "Ê‡",
            "v", "ÊŒ", "w", "Ê", "y", "Ê", '{', '}', "â€¿", "â€",
            "â…", "â†", "âˆ´", "âˆµ"
        );
        if %mappings{$char}:exists {
            $result = %mappings{$char};
        } else {
            my %inverted = %mappings.invert;
            if %inverted{$char}:exists {
                $result = %inverted{$char};
            }
        }
        $result;
    }
);

BEGIN %posts = (
    'invert' => &flip
);

