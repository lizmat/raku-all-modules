#!/usr/bin/env perl6

use v6;
use File::Find;

my (@tags, $fullpack);

# package
constant ptagfield = ";\"\tp";

# subroutine
constant stagfield = ";\"\ts";

grammar Purl6 {
    
    regex fullpack { ((\w|'::')*) }

    token TOP {
        ^ <packages> | <subroutines> $
    }

    token packages {
        \s* (role|unit\s+class|unit\s+module|module|package|class) \s+ <fullpack>
    }

    token subroutines {
        \s* (multi\s+sub|submethod|sub|multi\s+method|method) \s+ ( \w+ '::')* (\!*(\w|\-)+)
    }

}

class Purl6::Actions {

    has $.file;

    method packages($/ is copy) {

        $fullpack =  $<fullpack>.Str;

        my $rest = $<>;
        $rest ~~ s:g/\-/\\-/; 

        if $fullpack {
            take "{$fullpack}\t{$.file}\t/$rest/{ptagfield}";
        }
    }

    method subroutines($/ is copy) {

        my $func = $/[2].Str;

        my $rest = $<>;
        $rest ~~ s:g/\-/\\-/;

        my $line = "$func\t{$.file}\t/^$rest/{stagfield}";

        if $fullpack {
            take "{$fullpack}::$line";
        }

        take $line;

    }
}

my @files = find( :dir("."), :type('file'), :name( rx/(.p(m|l|6)|pm6 )$/) ).Â».IO;

for @files -> $io {

    my $file = $io.relative;

    for $io.lines -> $line {
        next unless $line;

        @tags.append: gather Purl6.parse( $line, :actions( Purl6::Actions.new( :$file ) ) );

    }  
}

@tags = @tags.unique.sort;

'tags'.IO.spurt: @tags.join: "\n";
